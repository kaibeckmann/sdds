.template 0
#  This is a code generator built using the iMatix GSL code generation
#  language. See https://github.com/imatix/gsl for details.

#   Resolve XML includes
function resolve_profiles ()
    for project.include
        if defined (include.filename)
            my.profile_file = project.load_file (filename)
            if defined (my.profile_file)
# if there are includes within the profile spec
                for my.profile_file.include
                    my.topic_file = project.load_file (filename)
                    if defined (my.topic_file)
                        move my.topic_file after include
                    endif
                endfor
                for my.profile_file.role
                    move role after include
                endfor
                for my.profile_file.topic where count (project.role, role.topic = topic.name)
                    move topic after include
                endfor
                for my.profile_file.datatype
                    move datatype after include
                endfor
                for my.profile_file.enum
                    move enum after include
                endfor
                for my.profile_file.struct
                    move struct after include
                endfor
            else
                echo "E: error loading include file: $(filename): $(xml.error?)"
            endif
        endif
    endfor
endfunction
resolve_profiles ()

project.description ?= "$(string.trim (project.?""):left)"
project.iface ?= ""
project.ip ?= "fe80::10"
project.port ?= "23234"
project.generated_warning_header ?= "\
################################################################################
#  THIS FILE IS 100% GENERATED FOR sDDS; DO NOT EDIT EXCEPT EXPERIMENTALLY     #
#  Please refer to the README for information about making permanent changes.  #
################################################################################"
.endtemplate

.#  These scripts generate code independently from one another
.#  so are not run in any particular order other than alphabetically.
.gsl from "sdds_topic.gsl"
.gsl from "sdds_skeleton.gsl"
.gsl from "sdds_impl.gsl"
.gsl from "sdds_constants.gsl"
.if project.os = "linux"
.   gsl from "sdds_make_linux.gsl"
.elsif project.os = "netbsd"
.   gsl from "sdds_make_netbsd.gsl"
.elsif project.os = "contiki"
.   gsl from "sdds_make_contiki.gsl"
.elsif project.os = "riot"
.   gsl from "sdds_make_riot.gsl"
.else
.   echo "W: Cannot generate makefiles for '$(project.os)'. It's not supported by makefile generator!"
.endif
